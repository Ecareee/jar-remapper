import org.gradle.api.internal.artifacts.dependencies.DefaultFileCollectionDependency

plugins {
    id 'java-gradle-plugin'
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

compileTestJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    google()
}

// 居然把我自己的 javaparser 移除了，gradleApi 的还在
//configurations.configureEach {
//    exclude group: 'com.github.javaparser', module: 'javaparser-core'
//}

// 排除 gradleApi 和 gradleTestKit 自带的旧版本 javaparser，防止与我们自己的 javaparser-symbol-solver-core 冲突
def excluded = ['javaparser']

def filterFiles = { dep ->
    files(dep.files.findAll { file ->
        !excluded.any {
            file.name.contains(it)
        }
    })
}

def filteredGradleApi = filterFiles(dependencies.gradleApi())
def filteredGradleTestKit = filterFiles(dependencies.gradleTestKit())

// 移除 java-gradle-plugin 自动添加的 gradleApi 和 gradleTestKit
afterEvaluate {
    ['api', 'testImplementation'].each { configName ->
        configurations.getByName(configName).dependencies.removeIf { dep ->
            dep instanceof DefaultFileCollectionDependency &&
                    dep.files.any { file ->
                        excluded.any {
                            file.name.contains(it)
                        }
                    }
        }
    }
}

dependencies {
    // 过滤后的 gradleApi
    implementation filteredGradleApi

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // SpecialSource
    implementation 'net.md-5:SpecialSource:1.11.5'

    // ASM
    implementation 'org.ow2.asm:asm:9.9.1'
    implementation 'org.ow2.asm:asm-commons:9.9.1'
    implementation 'org.ow2.asm:asm-tree:9.9.1'

    // YAML
    implementation 'org.yaml:snakeyaml:2.5'

    // smali
    implementation 'com.android.tools.smali:smali:3.0.9'
    implementation 'com.android.tools.smali:smali-baksmali:3.0.9'
    implementation 'com.android.tools.smali:smali-dexlib2:3.0.9'

    // Java 重映射
    implementation 'com.github.javaparser:javaparser-core:3.28.0'
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.28.0'

    // 测试
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation filteredGradleTestKit
}

gradlePlugin {
    plugins {
        jarRemapperPlugin {
            id = 'com.ecaree.jarremapper'
            implementationClass = 'com.ecaree.jarremapper.JarRemapperPlugin'
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
}

tasks.named('test', Test) {
    useJUnitPlatform()

    testLogging {
        events('passed', 'skipped', 'failed')
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat = 'full'
    }

    reports {
        html.required = true
        junitXml.required = true
    }
}

tasks.named('jar', Jar) {
    manifest {
        attributes(
                'Implementation-Title': 'JarRemapper Gradle Plugin',
                'Implementation-Version': version,
                'Implementation-Vendor': group
        )
    }
}