import org.gradle.api.internal.artifacts.dependencies.DefaultFileCollectionDependency

plugins {
    id 'java-gradle-plugin'
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

compileTestJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    google()
}

// 居然把我自己的 javaparser 移除了，gradleApi() 的还在
//configurations.configureEach {
//    exclude group: 'com.github.javaparser', module: 'javaparser-core'
//}

/**
 * 排除 gradleApi() 和 gradleTestKit() 自带的旧版本 javaparser，防止与我们自己的 javaparser-symbol-solver-core 冲突
 * 以及 gradleApi() 和 gradleTestKit() 打包 gradle-api.jar 前自带的 slf4j，防止 slf4j 绑定到 gradle 内部的 logger 而不是我们的 log4j-slf4j2-impl 导致日志并未输出
 * 相关问题 https://discuss.gradle.org/t/logging-in-gradle-plugin/31685 https://github.com/gradle/gradle/issues/2408
 */
def excluded = [
        'javaparser',
        'slf4j-api'
]

def filterFiles = { fileCollection ->
    files(fileCollection.files.findAll { file ->
        !excluded.any {
            file.name.contains(it)
        }
    })
}

def filteredGradleApi = filterFiles(dependencies.gradleApi())
// gradleApi() 会把 slf4j 打包到 gradle-api.jar 中，要想排除 slf4j，就必须从 lib 目录过滤
def filteredGradleApiFromLib = filterFiles(
        fileTree("${gradle.gradleHomeDir}/lib") { include '*.jar' }
)
def filteredGradleTestKit = filterFiles(dependencies.gradleTestKit())
// gradleTestKit() 只比 gradleApi() 多了个 gradle-test-kit
def filteredGradleTestKitFromLib = files(
        filteredGradleApiFromLib,
        dependencies.gradleTestKit().files.find {
            it.name.contains('gradle-test-kit')
        }
)

// 移除 java-gradle-plugin 自动添加的 gradleApi() 和 gradleTestKit()
afterEvaluate {
    ['api', 'testImplementation'].each { configName ->
        configurations.getByName(configName).dependencies.removeIf { dep ->
            dep instanceof DefaultFileCollectionDependency &&
                    dep.files.any { file ->
                        excluded.any {
                            file.name.contains(it)
                        }
                    }
        }
    }
}

dependencies {
    // 过滤后的 gradleApi()
//    implementation filteredGradleApi
    implementation filteredGradleApiFromLib

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    // SpecialSource
    implementation 'net.md-5:SpecialSource:1.11.5'

    // ASM
    implementation 'org.ow2.asm:asm:9.9.1'
    implementation 'org.ow2.asm:asm-commons:9.9.1'
    implementation 'org.ow2.asm:asm-tree:9.9.1'

    // YAML
    implementation 'org.yaml:snakeyaml:2.5'

    // smali
    implementation 'com.android.tools.smali:smali:3.0.9'
    implementation 'com.android.tools.smali:smali-baksmali:3.0.9'
    implementation 'com.android.tools.smali:smali-dexlib2:3.0.9'

    // Java 重映射
    implementation 'com.github.javaparser:javaparser-core:3.28.0'
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.28.0'

    // 测试
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
//    testImplementation filteredGradleTestKit
    testImplementation filteredGradleTestKitFromLib

    // 日志
    implementation 'org.slf4j:slf4j-api:2.1.0-alpha1'
//    implementation 'ch.qos.logback:logback-classic:1.5.25' // 颜色配置麻烦
    runtimeOnly 'org.apache.logging.log4j:log4j-api:2.25.3'
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.25.3'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl:2.25.3'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-api:2.25.3'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.25.3'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl:2.25.3'
}

gradlePlugin {
    plugins {
        jarRemapperPlugin {
            id = 'com.ecaree.jarremapper'
            implementationClass = 'com.ecaree.jarremapper.JarRemapperPlugin'
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
}

tasks.named('test', Test) {
    useJUnitPlatform()

    testLogging {
        events('passed', 'skipped', 'failed')
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat = 'full'
    }

    reports {
        html.required = true
        junitXml.required = true
    }
}

tasks.named('jar', Jar) {
    manifest {
        attributes(
                'Implementation-Title': 'JarRemapper Gradle Plugin',
                'Implementation-Version': version,
                'Implementation-Vendor': group
        )
    }
}