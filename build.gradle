import org.gradle.api.internal.artifacts.dependencies.DefaultFileCollectionDependency

plugins {
    id 'java-gradle-plugin'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
    google()
}

// 居然把我自己的 javaparser 移除了，gradle api 的还在
//configurations.configureEach {
//    exclude group: 'com.github.javaparser', module: 'javaparser-core'
//}

// 移除 java-gradle-plugin 自动添加的 gradle api 和 gradleTestKit
afterEvaluate {
    configurations.api.dependencies.removeIf { dep ->
        dep instanceof DefaultFileCollectionDependency
    }
    configurations.testImplementation.dependencies.removeIf { dep ->
        dep instanceof DefaultFileCollectionDependency
    }
}

// 排除 gradle api 自带的旧版本 javaparser，防止与我们自己的 javaparser 冲突
def filteredGradleApi = files(
        file("${gradle.gradleHomeDir}/lib").listFiles().findAll {
            !it.name.contains('javaparser')
        }
)

// 同样处理 gradleTestKit
def filteredGradleTestKit = files(
        file("${gradle.gradleHomeDir}/lib").listFiles().findAll {
            !it.name.contains('javaparser')
        },
        file("${gradle.gradleHomeDir}/lib/plugins").listFiles().findAll {
            !it.name.contains('javaparser')
        }
)

dependencies {
    // 过滤后的 gradle api
    implementation filteredGradleApi

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // SpecialSource
    implementation 'net.md-5:SpecialSource:1.11.5'

    // ASM
    implementation 'org.ow2.asm:asm:9.9.1'
    implementation 'org.ow2.asm:asm-commons:9.9.1'
    implementation 'org.ow2.asm:asm-tree:9.9.1'

    // YAML
    implementation 'org.yaml:snakeyaml:2.5'

    // smali
    implementation 'com.android.tools.smali:smali:3.0.9'
    implementation 'com.android.tools.smali:smali-baksmali:3.0.9'
    implementation 'com.android.tools.smali:smali-dexlib2:3.0.9'

    // Java 重映射
    implementation 'com.github.javaparser:javaparser-core:3.28.0'
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.28.0'

    // 测试
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.14.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.14.2"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.14.2"
    testRuntimeOnly "org.junit.platform:junit-platform-engine:1.14.2"
    testImplementation filteredGradleTestKit
}

gradlePlugin {
    plugins {
        jarRemapperPlugin {
            id = 'com.ecaree.jarremapper'
            implementationClass = 'com.ecaree.jarremapper.JarRemapperPlugin'
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
}

tasks.named('test', Test) {
    useJUnitPlatform()

    testLogging {
        events("passed", "skipped", "failed")
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat = 'full'
    }

    reports {
        html.required = true
        junitXml.required = true
    }
}


tasks.named('jar', Jar) {
    manifest {
        attributes(
                'Implementation-Title': 'JarRemapper Gradle Plugin',
                'Implementation-Version': version,
                'Implementation-Vendor': group
        )
    }
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

tasks.register('javadocJar', Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}